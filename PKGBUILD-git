# Maintainer: GaÃ«l PORTAY <gael.portay@gmail.com>

pkgname=(dosh-git docker-shell-git dosh-cqfd-git docker-cqfd-git)
pkgver=master
pkgrel=1
pkgdesc='Docker shell'
arch=('any')
url="https://github.com/gportay/$pkgname"
license=('LGPL')
makedepends=('asciidoctor')
checkdepends=('shellcheck')
source=("$pkgname::git+https://github.com/gportay/${pkgname%-git}.git"
	"bash-completion-cqfd-git::https://raw.githubusercontent.com/savoirfairelinux/cqfd/master/bash-completion")
sha256sums=('SKIP'
            'SKIP')
validpgpkeys=('8F3491E60E62695ED780AC672FA122CA0501CA71')

pkgver() {
	cd "$srcdir/$pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/$pkgname"
	make doc SHELL="/bin/sh"
}

check() {
	cd "$srcdir/$pkgname"
	make -k check
}

package_dosh-git() {
	depends=(docker)
	optdepends+=(docker-shell)
	provides=("${pkgname%-git}")
	conflicts=("${pkgname%-git}")

	cd "$srcdir/$pkgname"
	make DESTDIR="$pkgdir" PREFIX="/usr" install install-doc install-bash-completion
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_docker-shell-git() {
	pkgdesc='Docker CLI plugin for dosh'
	depends=(dosh)
	provides=("${pkgname%-git}")
	conflicts=("${pkgname%-git}")

	cd "$srcdir/dosh-git"
	make DESTDIR="$pkgdir/" PREFIX="/usr" install-cli-plugin
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_dosh-cqfd-git() {
	pkgdesc='A tool to wrap commands in controlled Docker containers using dosh'
	depends=(dosh)
	provides=("cqfd")
	conflicts=("cqfd")

	cd "$srcdir/dosh-git"
	make DESTDIR="$pkgdir/" PREFIX="/usr" install-cqfd
	if completionsdir="$(pkg-config --define-variable=prefix=/usr --variable=completionsdir bash-completion)"; then
		install -D -m 644 ${startdir}/bash-completion-cqfd-git "$pkgdir$completionsdir/cqfd"
	fi
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_docker-cqfd-git() {
	pkgdesc='Docker CLI plugin for cqfd'
	depends=(cqfd)
	provides=("${pkgname%-git}")
	conflicts=("${pkgname%-git}")

	cd "$srcdir/dosh-git"
	make DESTDIR="$pkgdir/" PREFIX="/usr" install-cli-plugin-cqfd
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# Maintainer: GaÃ«l PORTAY <gael.portay@gmail.com>

pkgname=(dosh-git
	 dosh-docker-shell-git
	 dosh-linux-amd64-git
	 dosh-linux-arm64-git
	 dosh-linux-arm-git
	 dosh-linux-ppc64le-git
	 dosh-linux-riscv64-git
	 dosh-linux-s390x-git
	 dosh-cqfd-git
	 dosh-docker-cqfd-git)
pkgver=master
pkgrel=1
pkgdesc='Docker shell'
arch=(any)
url=https://github.com/gportay/dosh
license=(LGPL-2.1-or-later)
depends=(bash)
makedepends=(asciidoctor bash-completion git)
checkdepends=(shellcheck)
source=(dosh-git::git+https://github.com/gportay/dosh.git
	bash-completion-cqfd-git::https://raw.githubusercontent.com/savoirfairelinux/cqfd/master/bash-completion)
sha256sums=(SKIP
            SKIP)
validpgpkeys=(8F3491E60E62695ED780AC672FA122CA0501CA71)

pkgver() {
	cd "$srcdir/dosh-git"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/dosh-git"
	make cqfd.1.gz cqfdrc.5.gz dosh.1.gz
}

check() {
	cd "$srcdir/dosh-git"
	make -k check
}

package_dosh-git() {
	depends+=(docker)
	optdepends+=(dosh-docker-shell)
	provides=(dosh)
	conflicts=(dosh)

	cd "$srcdir/dosh-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install install-doc install-bash-completion
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/dosh-git/LICENSE"
}

package_dosh-docker-shell-git() {
	pkgdesc='Docker CLI plugin for dosh'
	depends+=(dosh)
	provides=(dosh-docker-shell)
	conflicts=(dosh-docker-shell)

	cd "$srcdir/dosh-git"
	make DESTDIR="$pkgdir/" PREFIX="/usr" install-docker-cli-plugin
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/dosh-docker-shell-git/LICENSE"
}

package_dosh-linux-amd64-git() {
	pkgdesc='Docker shell for linux/amd64 platform'
	depends+=(dosh)
	provides=(dosh-linux-amd64)
	conflicts=(dosh-linux-amd64)

	cd "$srcdir/dosh-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install-linux-amd64-dosh
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/dosh-linux-amd64-git/LICENSE"
}

package_dosh-linux-arm64-git() {
	pkgdesc='Docker shell for linux/arm64 platform'
	depends+=(dosh)
	provides=(dosh-linux-arm64)
	conflicts=(dosh-linux-arm64)

	cd "$srcdir/dosh-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install-linux-arm64-dosh
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/dosh-linux-arm64-git/LICENSE"
}

package_dosh-linux-arm-git() {
	pkgdesc='Docker shell for linux/arm platform'
	depends+=(dosh)
	provides=(dosh-linux-arm)
	conflicts=(dosh-linux-arm)

	cd "$srcdir/dosh-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install-linux-arm-dosh install-linux-arm-v6-dosh install-linux-arm-v7-dosh
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/dosh-linux-arm-git/LICENSE"
}

package_dosh-linux-ppc64le-git() {
	pkgdesc='Docker shell for linux/ppc64le platform'
	depends+=(dosh)
	provides=(dosh-linux-ppc64le)
	conflicts=(dosh-linux-ppc64le)

	cd "$srcdir/dosh-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install-linux-ppc64le-dosh
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/dosh-linux-ppc64le-git/LICENSE"
}

package_dosh-linux-riscv64-git() {
	pkgdesc='Docker shell for linux/riscv64 platform'
	depends+=(dosh)
	provides=(dosh-linux-riscv64)
	conflicts=(dosh-linux-riscv64)

	cd "$srcdir/dosh-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install-linux-riscv64-dosh
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/dosh-linux-riscv64-git/LICENSE"
}

package_dosh-linux-s390x-git() {
	pkgdesc='Docker shell for linux/s390x platform'
	depends+=(dosh)
	provides=(dosh-linux-s390x)
	conflicts=(dosh-linux-s390x)

	cd "$srcdir/dosh-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install-linux-s390x-dosh
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/dosh-linux-s390x-git/LICENSE"
}

package_dosh-cqfd-git() {
	pkgdesc='Wrap commands in controlled Docker containers using dosh'
	depends+=(dosh)
	provides=(dosh-cqfd)
	conflicts=(dosh-cqfd)

	cd "$srcdir/dosh-git"
	make DESTDIR="$pkgdir/" PREFIX="/usr" install-cqfd
	install -D -m 644 "$startdir/bash-completion-cqfd-git" "$pkgdir$completionsdir/cqfd"
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/dosh-cqfd-git/LICENSE"
}

package_dosh-docker-cqfd-git() {
	pkgdesc='Docker CLI plugin for cqfd'
	depends+=(dosh-cqfd)
	provides=(dosh-docker-cqfd)
	conflicts=(dosh-docker-cqfd)

	cd "$srcdir/dosh-git"
	make DESTDIR="$pkgdir/" PREFIX="/usr" install-docker-cli-plugin-cqfd
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/dosh-docker-cqfd-git/LICENSE"
}

# Maintainer: GaÃ«l PORTAY <gael.portay@gmail.com>

pkgname=(dosh-git dosh-cqfd-git)
pkgver=master
pkgrel=1
pkgdesc='Docker shell'
arch=(any)
url=https://github.com/gportay/dosh
license=(LGPL-2.1-or-later)
depends=(bash)
makedepends=(asciidoctor bash-completion git)
checkdepends=(shellcheck)
source=(dosh-git::git+https://github.com/gportay/dosh.git
	bash-completion-cqfd-git::https://raw.githubusercontent.com/savoirfairelinux/cqfd/master/bash-completion)
sha256sums=(SKIP
            SKIP)
validpgpkeys=(8F3491E60E62695ED780AC672FA122CA0501CA71)
changelog=CHANGELOG.md

pkgver() {
	cd "$srcdir/dosh-git"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/dosh-git"
	make cqfd.1.gz cqfdrc.5.gz dosh.1.gz
}

check() {
	cd "$srcdir/dosh-git"
	make -k check
}

package_dosh-git() {
	depends+=(docker)
	provides=(dosh)
	conflicts=(dosh)

	cd "$srcdir/dosh-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install install-doc install-bash-completion install-docker-cli-plugin install-linux-platforms
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/dosh-git/LICENSE"
}

package_dosh-cqfd-git() {
	pkgdesc='Wrap commands in controlled Docker containers using dosh'
	depends+=(dosh)
	provides=(dosh-cqfd)
	conflicts=(dosh-cqfd)

	cd "$srcdir/dosh-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install-cqfd install-docker-cli-plugin-cqfd
	install -D -m 644 "$startdir/bash-completion-cqfd-git" "$pkgdir$completionsdir/cqfd"
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/dosh-cqfd-git/LICENSE"
}
